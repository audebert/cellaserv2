# Maintainer: RÃ©mi Audebert <contact@halfr.net>

pkgname=cellaserv2
pkgver=
pkgrel=1
pkgdesc="cellaserv2 rpc broker"
arch=('x86_64' 'i686' 'arm')
url="http://code.evolutek.org/cellaserv2"
license=('beerware')
makedepends=('go' 'git')
source=("$pkgname::git+http://bitbucket.org/evolutek/cellaserv2.git")
options=('!strip' '!emptydirs')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$pkgname"
  GOPATH="$srcdir" go get -d -v
  GOPATH="$srcdir" go build -v
}

package() {
  cd "$srcdir/$pkgname"
  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 systemd/cellaserv2.service "$pkgdir/usr/lib/systemd/system/cellaserv2.service"
  install -Dm644 systemd/evolutek.target "$pkgdir/usr/lib/systemd/system/evolutek.target"
}

# vim:set ts=2 sw=2 et:
